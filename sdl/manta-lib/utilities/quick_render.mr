module {
    @name:          "Quick Render"
    @project:       "MantaRay"
    @author:        "Ange Yaghi"
    @maintainer:    "Ange Yaghi"
    @copyright:     "Copyright 2019, Ange Yaghi"
    @doc:           "Defines some utilities for making quick renders"
    @version:       "0.0.1a"
    @github:        "github.com/ange-yaghi/manta-ray"
}

private import "console.mr"
private import "paths.mr"

private import "../types/atomic_types.mr"
private import "../types/conversions.mr"
private import "../types/operations.mr"
private import "../surface/materials/material_library.mr"
private import "../surface/materials/bsdf_material.mr"
private import "../surface/bsdf/lambertian_brdf.mr"
private import "../surface/bsdf/bxdf_to_bsdf.mr"
private import "../camera/utilities.mr"
private import "../camera/cameras.mr"
private import "../scene/scene_object.mr"
private import "../scene/scene_geometry.mr"
private import "../scene/scene.mr"
private import "../acceleration/kd_tree.mr"
private import "../dates/dates.mr"
private import "../ray-tracer/ray_tracer.mr"
private import "../ray-tracer/image_plane.mr"
private import "../signal-processing/samplers/samplers.mr"
private import "../image/image_output.mr"
private import "../mesh/obj_file.mr"

public node quick_render {
    input object_file;
    input materials:        default_library;
    input default_material: "Default";
    input camera_position:  vector(5, 5, 5);
    input camera_target:    vector(0, 0, 0);
    input camera_up:        vector(0, 1, 0);
    input background:       1.0;
    input samples:          128;
    input sampler:          random_sampler(samples);
    input resolution_x:     1920;
    input resolution_y:     1080;
    input plane_height:     1.0;
    input plane_distance:   1.0;
    input output_name:      "quick_render";
    input image_plane:      image_plane(width: resolution_x, height: resolution_y);
    input output_dir:       "";
    input scene:            scene();

    // Save the current date/time
    current_date current_date()

    log("\n")
    log("////////////////////////////////////////////////\n")
    log("\n")
    log("  Mantaray Quick Render\n")
    log("\n")
    log("////////////////////////////////////////////////\n")
    log("Initializing...\n")

    // Set up the camera
    camera_target camera_controller(
        position:   camera_position,
        target:     camera_target,
        up:         camera_up
    )

    standard_camera camera(
        up:             camera_controller.up,
        position:       camera_controller.position,
        direction:      camera_controller.direction,
        image_plane:    image_plane,
        plane_height:   plane_height,
        plane_distance: plane_distance
    )

    // Default material, matte white
    material_library default_library()
    bsdf_material(
        name: "Default",
        library: default_library,
        bsdf: lambertian_brdf(),
        reflectance: 0.5
    )

    // Load the scene
    log("Loading the scene...\n")
    obj_file scene_mesh(
        filename: object_file,
        materials: materials,
        default_material: default_material
    )

    scene_object main_object(
        name: "Main",
        scene: scene,
        geometry: kd_tree(scene_mesh)
    )

    // Render the scene
    ray_tracer mantaray(
        scene: scene,
        camera: camera,
        materials: materials,
        background: background,
        sampler: sampler,
        deterministic_seed: true,
        threads: 12
    )

    log("Render complete\n")
    log("Writing render to file\n")

    image_output(
        map: mantaray.image,
        jpeg_quality: 100,
        filename: 
            output_dir + current_date.as_string + "_" + output_name + 
            "_S" + string(samples) + ".jpg"
    )
}
